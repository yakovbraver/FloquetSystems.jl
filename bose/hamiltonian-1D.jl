using SparseArrays, Combinatorics, SpecialFunctions
using DifferentialEquations, FLoops
using LinearAlgebra: diagind, eigvals, I
using ProgressMeter: @showprogress

import Base.show

"""
A type representing a Bose-Hubbard Hamiltonian,
    H = - ‚àë ùêΩ·µ¢‚±º ùëé‚Ä†·µ¢ ùëé‚±º, + ùëà/2 ‚àë ùëõ·µ¢(ùëõ·µ¢ - 1).
"""
mutable struct BoseHamiltonian
    J::Float64
    U::Float64
    f::Float64 # F / œâ
    œâ::Real
    ncells::Int
    nbozons::Int
    H::SparseMatrixCSC{ComplexF64, Int} # the Hamiltonian matrix
    basis_states::Vector{Vector{Int}}     # all basis states as a vector (index => state)
    index_of_state::Dict{Vector{Int},Int} # a dictionary (state => index)
end

"Construct a `BoseHamiltonian` object defined on `lattice`."
function BoseHamiltonian(J::Real, U::Real, f::Real, œâ::Real, ncells::Integer, nbozons::Integer; isperiodic::Bool, order::Integer=1)
    nstates = binomial(nbozons+ncells-1, nbozons)
    bh = BoseHamiltonian(float(J), float(U), float(f), float(œâ), ncells, nbozons, spzeros(Float64, 1, 1), Vector{Vector{Int}}(undef, nstates), Dict{Vector{Int},Int}())
    makebasis!(bh)
    # constructH!(bh, isperiodic, order)
    constructH_U!(bh, isperiodic, order)
    bh
end

"Construct the Hamiltonian matrix."
function constructH!(bh::BoseHamiltonian, isperiodic::Bool, order::Integer)
    H_rows, H_cols, H_vals = Int[], Int[], Float64[]
    (;J, U, f, œâ) = bh
    Jeff = J * besselj0(f)

    J_sum = [0.0, 0.0]
    if order == 3
        a_max = 20
        J_sum[1] = (J/œâ)^2 * U * sum(         besselj(a, f)^2 / a^2 for a in 1:a_max) # for ùëó = k
        J_sum[2] = (J/œâ)^2 * U * sum((-1)^a * besselj(a, f)^2 / a^2 for a in 1:a_max) # for ùëó ‚â† k
    end

    # take each basis state and find which transitions are possible
    for (state, index) in bh.index_of_state
        val_d = 0.0 # diagonal value
        for i = 1:bh.ncells # iterate over the terms of the Hamiltonian
            # ùëõ·µ¢(ùëõ·µ¢ - 1)
            if (state[i] > 1)
                val_d += bh.U/2 * state[i] * (state[i] - 1)
            end
            # ùëé‚Ä†·µ¢ ùëé‚±º
            for j in (i-1, i+1)
                if j == 0
                    !isperiodic && continue
                    j = bh.ncells
                elseif j == bh.ncells + 1
                    !isperiodic && continue
                    j = 1
                end
                if (state[j] > 0) # check that a particle is present at site `j` so that destruction ùëé‚±º is possible
                    val = -Jeff * sqrt( (state[i]+1) * state[j] )
                    bra = copy(state)
                    bra[j] -= 1
                    bra[i] += 1
                    row = bh.index_of_state[bra]
                    push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                end
            end

            if order == 3
                for j in (i-1, i+1), k in (i-1, i+1)
                    if j == 0
                        !isperiodic && continue
                        j = bh.ncells
                    elseif j == bh.ncells + 1
                        !isperiodic && continue
                        j = 1
                    end
                    if k == 0
                        !isperiodic && continue
                        k = bh.ncells
                    elseif k == bh.ncells + 1
                        !isperiodic && continue
                        k = 1
                    end
                    C‚ÇÅ, C‚ÇÇ = j == k ? J_sum : (J_sum[2], J_sum[1])
                    # ùëé‚Ä†‚Çñ (2ùëõ·µ¢ - ùëõ‚±º) ùëé‚±º
                    if (state[j] > 0 && state[i] != state[j]-1)
                        val = C‚ÇÅ * ‚àö( (k == j ? state[k] : state[k]+1) * state[j] ) * (2state[i] - (state[j]-1))
                        bra = copy(state)
                        bra[j] -= 1
                        bra[k] += 1
                        row = bh.index_of_state[bra]
                        push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                    end
                    # ùëé‚Ä†‚±º (2ùëõ·µ¢ - ùëõ‚±º) ùëé‚Çñ
                    if (state[k] > 0 && state[i] != (j == k ? state[j]-1 : state[j]))
                        val = C‚ÇÅ * ‚àö( (j == k ? state[j] : state[j]+1) * state[k] ) * (2state[i] - (j == k ? state[j]-1 : state[j]))
                        bra = copy(state)
                        bra[k] -= 1
                        bra[j] += 1
                        row = bh.index_of_state[bra]
                        push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                    end
                    # ùëé‚Ä†·µ¢ ùëé‚Ä†·µ¢ ùëé‚Çñ ùëé‚±º
                    if ( (k == j && state[j] > 1) || (k != j && state[k] > 0 && state[j] > 0))
                        val = -C‚ÇÇ * ‚àö( (state[i]+2) * (state[i]+1) * (k == j ? (state[j]-1)state[j] : state[j]state[k]))
                        bra = copy(state)
                        bra[j] -= 1
                        bra[k] -= 1
                        bra[i] += 2
                        row = bh.index_of_state[bra]
                        push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                    end
                    # ùëé‚Ä†‚Çñ ùëé‚Ä†‚±º ùëé·µ¢ ùëé·µ¢
                    if (state[i] > 1)
                        val = -C‚ÇÇ * ‚àö( (k == j ? (state[j]+2) * (state[j]+1) : (state[k]+1) * (state[j]+1)) * (state[i]-1)state[i])
                        bra = copy(state)
                        bra[i] -= 2
                        bra[j] += 1
                        bra[k] += 1
                        row = bh.index_of_state[bra]
                        push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                    end
                end
            end
        end
        push_state!(H_rows, H_cols, H_vals, val_d; row=index, col=index)
    end
    bh.H = sparse(H_rows, H_cols, H_vals)
end

"Construct the Hamiltonian matrix."
function constructH_U!(bh::BoseHamiltonian, isperiodic::Bool, order::Integer)
    H_rows, H_cols, H_vals = Int[], Int[], Float64[]
    (;J, U, f, œâ) = bh
    Jeff = J * besselj0(f)

    n_max = bh.nbozons + 1
    n_min = -bh.nbozons - 3
    R1 = Dict{Int64, Real}()
    R2 = Dict{Int64, Real}()
    for n in n_min:n_max
        R1[n] = ùëÖ(œâ, U*n, type=1)
        R2[n] = ùëÖ(œâ, U*n, type=2)
    end

    # take each basis state and find which transitions are possible
    for (state, index) in bh.index_of_state
        val_d = 0.0 # diagonal value
        for i = 1:bh.ncells # iterate over the terms of the Hamiltonian
            # ùëõ·µ¢(ùëõ·µ¢ - 1)
            if (state[i] > 1) # check that at least two particles are present at site `i` so that destruction ùëé‚±ºùëé‚±º is possible
                val_d += bh.U/2 * state[i] * (state[i] - 1)
            end
            # ùëé‚Ä†·µ¢ ùëé‚±º
            for j in (i-1, i+1)
                if j == 0
                    !isperiodic && continue
                    j = bh.ncells
                elseif j == bh.ncells + 1
                    !isperiodic && continue
                    j = 1
                end
                if (state[j] > 0) # check that a particle is present at site `j` so that destruction ùëé‚±º is possible
                    val = -Jeff * sqrt( (state[i]+1) * state[j] )
                    bra = copy(state)
                    bra[j] -= 1
                    bra[i] += 1
                    row = bh.index_of_state[bra]
                    push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                end
            end

            if order == 2
                for (j, j‚ÇÇ, k) in zip((i-1, i+1), (i-2, i+2), (i+1, i-1))
                    if j == 0
                        j = bh.ncells
                    elseif j == bh.ncells + 1
                        j = 1
                    elseif k == 0
                        k = bh.ncells
                    elseif k == bh.ncells + 1
                        k = 1
                    end
                    if j‚ÇÇ < 1
                        j‚ÇÇ = bh.ncells + j‚ÇÇ
                    elseif j‚ÇÇ > bh.ncells
                        j‚ÇÇ = j‚ÇÇ - bh.ncells
                    end

                    if (state[j] > 1)
                        # ùëé‚Ä†·µ¢ ùëé‚Ä†·µ¢ ùëé‚±º ùëé‚±º
                        n = state[i]+2 - (state[j]-2)
                        val = -J/2 * (R1[n - 3] - R1[n - 1]) * ‚àö((state[i]+1) * (state[i]+2) * state[j] * (state[j]-1))
                        bra = copy(state)
                        bra[j] -= 2
                        bra[i] += 2
                        row = bh.index_of_state[bra]
                        push_state!(H_rows, H_cols, H_vals, val; row, col=index)

                        # ùëé‚Ä†·µ¢ ùëé‚Ä†‚±º‚ÇÇ ùëé‚±º ùëé‚±º
                        n = state[i]+1 - (state[j]-2)
                        val = -J/2 * (R2[n - 2] - R2[n - 1]) * ‚àö((state[j‚ÇÇ]+1) * (state[i]+1) * state[j] * (state[j]-1))
                        bra = copy(state)
                        bra[j] -= 2
                        bra[i] += 1
                        bra[j‚ÇÇ] += 1
                        row = bh.index_of_state[bra]
                        push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                    end
                    # ùëé‚Ä†·µ¢ ùëé‚Ä†·µ¢ ùëé‚±º ùëé‚Çñ
                    if (state[j] > 0 && state[k] > 0)
                        n = state[i]+2 - (state[j]-1)
                        val = -J/2 * (R2[n - 2] - R2[n - 1]) * ‚àö((state[i]+1) * (state[i]+2) * state[j] * state[k])
                        bra = copy(state)
                        bra[j] -= 1
                        bra[k] -= 1
                        bra[i] += 2
                        row = bh.index_of_state[bra]
                        push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                    end
                    # ùëé‚Ä†‚Çñ (ùëõ·µ¢ + 1) ùëé‚±º - ùëé‚Ä†‚Çñ ùëõ·µ¢ ùëé‚±º
                    if (state[j] > 0)
                        n = state[i] - (state[j]-1)
                        val = -J/2 * R1[n] * (state[i] + 1) * ‚àö((state[k]+1) * state[j])
                              +J/2 * R1[n - 1] * state[i] * ‚àö((state[k]+1) * state[j])
                        bra = copy(state)
                        bra[j] -= 1
                        bra[k] += 1
                        row = bh.index_of_state[bra]
                        push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                    end
                    # ùëé‚Ä†·µ¢ ùëõ‚±º ùëé‚±º‚ÇÇ - ùëé‚Ä†·µ¢ (ùëõ‚±º + 1) ùëé‚±º‚ÇÇ
                    if (state[j‚ÇÇ] > 0)
                        n = state[i]+1 - state[j]
                        val = -J/2 * R1[n] * state[j] * ‚àö((state[i]+1) * state[j‚ÇÇ])
                              +J/2 * R1[n - 1] * (state[j] + 1) * ‚àö((state[i]+1) * state[j‚ÇÇ])
                        bra = copy(state)
                        bra[j‚ÇÇ] -= 1
                        bra[i] += 1
                        row = bh.index_of_state[bra]
                        push_state!(H_rows, H_cols, H_vals, val; row, col=index)
                    end
                    # ùëõ‚±º (ùëõ·µ¢ + 1) - (ùëõ‚±º + 1) ùëõ·µ¢
                    if state[i] > 0 && state[j] > 0
                        n = state[i] - state[j]
                        val = -J/2 * R2[n + 1] * state[j] * (state[i]+1)
                            +J/2 * R2[n - 1] * (state[j] + 1) * state[i]
                        push_state!(H_rows, H_cols, H_vals, val; row=index, col=index)
                    end
                end
            end
        end
        push_state!(H_rows, H_cols, H_vals, val_d; row=index, col=index)
    end
    bh.H = sparse(H_rows, H_cols, H_vals)
end

function ùëÖ(œâ::Real, Un::Real; type::Integer)
    N = 5
    a‚ÇÄ = round(Int, -Un / œâ)
    # if `Un / œâ` is integer, a‚ÇÄ should be skipped in the sum
    a_range = isinteger(Un / œâ) ? [a‚ÇÄ-N:a‚ÇÄ-1; a‚ÇÄ+1:a‚ÇÄ+N] : collect(a‚ÇÄ-N:a‚ÇÄ+N) # collect for type stability
    r = 0.0
    if type == 1
        for a in a_range
            a == 0 && continue
            r += 1/(a*œâ + Un) * besselj(a, 1)^2 * (-1)^a
        end
    else
        for a in a_range
            a == 0 && continue
            r += 1/(a*œâ + Un) * besselj(a, 1)^2
        end
    end
    return r
end

function push_state!(H_rows, H_cols, H_vals, val; row, col)
    push!(H_vals, val)
    push!(H_cols, col)
    push!(H_rows, row)
end

"""
Generate all possible combinations of placing the bozons in the lattice.
Populate `bh.basis_states` and `bh.index_of_state`.
"""
function makebasis!(bh::BoseHamiltonian)
    index = 1 # unique index identifying the state
    (;ncells, nbozons) = bh
    for partition in integer_partitions(nbozons)
        length(partition) > ncells && continue # Example (nbozons = 3, ncells = 2): partition = [[3,0], [2,1], [1,1,1]] -- skip [1,1,1] as impossible
        append!(partition, zeros(ncells-length(partition)))
        for p in multiset_permutations(partition, ncells)
            bh.index_of_state[p] = index
            bh.basis_states[index] = p
            index += 1
        end
    end
end

"Print non-zero elements of the Hamiltonian `bh` in the format ‚ü®bra| HÃÇ |ket‚ü©."
function Base.show(io::IO, bh::BoseHamiltonian)
    H_rows, H_cols, H_vals = findnz(bh.H)
    for (i, j, val) in zip(H_rows, H_cols, H_vals) # iterate over the terms of the Hamiltonian
        println(io, bh.basis_states[i], " HÃÇ ", bh.basis_states[j], " = ", round(val, sigdigits=3))
    end
end

"""
Calculate quasienergy spectrum of `bh` via monodromy matrix for each value of ùëà in `Us`.
Passed `bh` should correrspond to ùëà = 1 and ùêπ = 0.
"""
function quasienergy(bh::BoseHamiltonian, F::Real, œâ::Real, Us::AbstractVector{<:Real})
    n_levels = size(bh.H, 1)
    n_U = length(Us)

    T = 2œÄ / œâ
    tspan = (0.0, T)
    
    Œµ = Matrix{Float64}(undef, n_levels, n_U)
    C‚ÇÄ = Matrix{ComplexF64}(I, n_levels, n_levels)

    H = copy(bh.H)
    di = diagind(H)
    inter_term = H[di] # interaction term ùëà/2 ‚àë ùëõ·µ¢(ùëõ·µ¢ - 1) for ùëà = 1

    drive_term = similar(inter_term)
    for (state, index) in bh.index_of_state
        drive_term[index] = sum(F * j * state[j] for j in eachindex(state)) # ‚ü®s| ‚àë ùêπùëóùëõ‚±º |s‚ü©
    end

    H .*= -im # as on the lhs of the Schr√∂dinger equation
    @showprogress for (i, U) in enumerate(Us)
        params = (di, inter_term, U, drive_term, œâ)
        H_op = DiffEqArrayOperator(H, update_func=update_func!)
        prob = ODEProblem(H_op, C‚ÇÄ, tspan, params, save_everystep=false)
        sol = solve(prob, MagnusGauss4(), dt=T/100)
        Œµ[:, i] = -œâ .* angle.(eigvals(sol[end])) ./ 2œÄ
    end

    return Œµ
end

"Update rhs operator (used for monodromy matrix calculation)."
function update_func!(H, u, p, t)
    di, inter_term, U, drive_term, œâ = p
    @. H[di] .= -im * (inter_term * U + drive_term * cos(œâ*t))
end